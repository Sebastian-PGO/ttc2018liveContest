import graph as graph
import group
import print

// `id` must be u64 because some ids use very big numbers.
input relation User(id: u64, name: string)
input relation Comments(id: u64, timestamp: string, content: string, creator: u64, parent: u64)
input relation Posts(id: u64, timestamp: string, content: string, submitter: u64)

input relation Submitter(postid: u64, userid: u64)
input relation Submissions(userid: u64, postid: u64)
input relation Friend(src: u64, dst: u64)

input relation LikedBy(srcComment: string, dstUser: u64)
input relation Likes(srcUser: u64, dstComment: u64)

// Two persons like the same comment and they are friends.
output relation LikesSameComment(comment: u64, p1: u64, p2: u64)
// The lowest person in the strongly connected components.
output relation LowestFriend(lowestPerson: u64, other: u64, comment: u64)

// Output relations for Q2.
// Likes(user, comment) is the node in the graph and edges are drawn between nodes. 
output relation UserLikesNode(user: u64, comment: u64)
output relation UserLikesEdge(src: UserLikesNode, dst: UserLikesNode)
// Calculate the size of each component that has the same anchor as a node `Likes` which contains the comment Id and user Id.
output relation SCC(anchor: UserLikesNode, size: u64)
// One comment may have several groups of people who like the comment and form a strongly connected component.
// e.g. A, B, C like comment one and D, E like the comment one too but are not connected to the previous group of people,
// then comment one has two strongly connected components and the score = 2*2 + 3*3 = 13.
// Also need to include the timestamp together with score for sorting in case multiple comments have the same score.
output relation SCCScore(score: u64, comment: u64)
output relation SCCScoreTimestamp(score: u64, timestamp: string, comment: u64)
output relation Top3CommentScore(first: u64, second: u64, third: u64)

// Q2: The top 3 most influential comments sorted by timestamp. 
// LikesSameComment(comment, u1, u1) :- Likes(u1, comment). // This rule is not necessary.
// LikesSameComment(comment, u1, u2), LikesSameComment(comment, u2, u1) :- Likes(u1, comment), Likes(u2, comment), Friend(u1, u2).
// LikesSameComment(comment, u1, u3) :- LikesSameComment(comment, u1, u2), LikesSameComment(comment, u2, u3).

// 1. Use DDLog syntax to express SCC.
// The score for each comment is the square of the size of the strongly connected components. 
// SCCScore(cid, score) :- LikesSameComment(cid, u1, u2), 
    // var people = Aggregate((cid), group_count(u1)), var score = people * people.

// 2. Use transformer to express SCC with native differential-dataflow implementation directly applied on some relations.
// What if only two comments are liked but we need three top scores? If you comment then I assume you like your own comment.

UserLikesNode(u, cid) :- Likes(u, cid).

// Add self-loop to every node in the graph.
UserLikesEdge(n, n) :- n in UserLikesNode(u, comment). 
UserLikesEdge(n1, n2) :- n1 in UserLikesNode(u1, comment), n2 in UserLikesNode(u2, comment), Friend(u1, u2),
    // Inspect print("UserLikesEdge - Comment id is ${comment} with user1 ${u1} and user2 ${u2}"),
    .

function convert_like1(ule: UserLikesEdge): UserLikesNode { ule.src }
function convert_like2(ule: UserLikesEdge): UserLikesNode { ule.dst }

// The result is a tuple of node Id and the lowest node Id in the group as an anchor.
output relation SCCLabel[(UserLikesNode, UserLikesNode)]
apply graph::SCC(UserLikesEdge, convert_like1, convert_like2) -> (SCCLabel)

// Each component is represented by an anchor followed by the size of component.
SCC(anchor, size) :- SCCLabel[(likes, likes_lowest)], 
    var group = (likes).group_by(likes_lowest),
    var anchor = group.group_key(),
    var size = group.group_count_distinct(),
    // Inspect print("SCC - Anchor is comment: ${anchor.comment} and user: ${anchor.user} with scc size ${size}")
    .

// What if there are less than three comments that have a score? 
SCCScore(sum, comment_key) :- SCC(anchor, size),
    var comment = anchor.comment,
    var user = anchor.user,
    // Inspect print("SCC - Anchor is comment: ${comment} and user: ${user} with scc size ${size}"),
    var group = (user, size).group_by(comment),
    var comment_key = group.group_key(),
    var sum = group.sum_of(|tuple| tuple.1 * tuple.1),
    // var group_size = group.group_count_distinct(),
    // Inspect print("SCCScore - Comment id is ${comment} with group size ${group_size} and scc score sum ${sum}"),
    .

// For comments that do not have likes, we give it a score of 0.
SCCScore(0, cid) :- Comments(cid,_,_,_,_), not Likes(_, cid).

SCCScoreTimestamp(score, timestamp, comment) :- SCCScore(score, comment), Comments(comment, timestamp,_,_,_),
    	// Inspect print("Comment id is ${comment} with score ${score} at time ${timestamp}")
        .

// An extern function that returns the top three items as option in the group.
extern function top_three(g: Group<'K, 'V>): ('V, 'V, 'V)

Top3CommentScore(first.comment, second.comment, third.comment) :- st in SCCScoreTimestamp(score, timestamp, comment),
    var group = (st).group_by(()),
    (var first, var second, var third) = group.top_three()
    .
