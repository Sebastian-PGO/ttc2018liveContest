plugins {
	id "org.xtext.xtend" version "2.0.4"
}

repositories {
	mavenCentral()
	flatDir {
		dirs	'libs',
			// generated code for the SocialNetwork metamodel must be imported so that our AOFAccessors active annotation can work
			"../EMFSolutionTemplate"   // TODO: change relative folder dependency to EMFSolutionTemplate into a project dependency?
	}
}

dependencies {
	compile name: 'aof'
	compile name: 'solution'
	compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.12.0'
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'application'

xtext.version = "2.12.0"

apply plugin: 'java-library-distribution'

xtend {
	sourceSets {
		main.xtendOutputDir = 'xtend-gen'
	}
}

mainClassName = 'ttc2018.LiveContestDriver'

sourceSets {
	main {
		java {
			srcDirs = ['src', 'src-gen']
		}
	}
}

// Temporary workaround for Xtext Java 8 dependency error
// TODO: remove when possible
// https://github.com/eclipse/xtext/issues/1976#issuecomment-864946993
// https://stackoverflow.com/questions/68041148/java-lang-unsupportedclassversionerror-in-gradle-build#comment120309128_68041868
configurations.all {
	resolutionStrategy {
		eachDependency { DependencyResolveDetails details ->
			if (details.requested.group == 'org.eclipse.platform' && details.requested.name == 'org.eclipse.equinox.common') {
				details.useVersion "3.13.0"
			}
		}
	}
}
